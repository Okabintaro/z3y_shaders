PROPERTIES_START // Properties
    [MainTexture] _MainTex ("Albedo", 2D) = "white" {}
    [MainColor] _Color ("Color", Color) = (1,1,1,1)
    _AlbedoSaturation ("Saturation", Float) = 1
    [NoScaleOffset] _MetallicGlossMap("Mask Map", 2D) = "white" {}
    _Metallic ("Metallic", Range(0,1)) = 0
    _Glossiness ("Smoothness", Range(0,1)) = 0.5
    [NoScaleOffset] [Normal] _BumpMap ("Normal Map", 2D) = "bump" {}
    _BumpScale ("Normal Scale", Float) = 1

    [Toggle(_EMISSION)] _EmissionToggle ("Enable Emission", Int) = 0
    [NoScaleOffset] _EmissionMap ("Emission Map", 2D) = "white" {}
    [HDR] _EmissionColor ("Emission Color", Color) = (0,0,0)

    _SpecularOcclusion ("Specular Occlusion", Range(0,1)) = 1
PROPERTIES_END


DEFINES_START // Keywords, Defines
    //#define VARYINGS_NEED_TEXCOORDX (0-3)
    #define ATTRIBUTES_NEED_TEXCOORD0
    //#define VARYINGS_NEED_COLOR
    //#define VARYINGS_NEED_INTERPX (0-4)
    #define VARYINGS_NEED_INTERP0
    //#define VARYINGS_NEED_CULLFACE
    #define DISABLE_LIGHT_PROBE_PROXY_VOLUME
DEFINES_END


CBUFFER_START // declare all Material properties excluding textures
    half4 _Color;
    half4 _MainTex_ST;
    half _Glossiness;
    half _Metallic;
    half _BumpScale;
    half _AlbedoSaturation;
    half3 _EmissionColor;
    half _SpecularOcclusion;
CBUFFER_END


CODE_START

    TEXTURE2D(_MainTex);
    TEXTURE2D(_BumpMap);
    TEXTURE2D(_MetallicGlossMap);
    TEXTURE2D(_EmissionMap);
    SAMPLER(sampler_MainTex);
    SAMPLER(sampler_BumpMap);
    SAMPLER(sampler_MetallicGlossMap);
    SAMPLER(sampler_EmissionMap);

    // Modify attributes before the VertexDescription
    #define USE_MODIFYATTRIBUTES
    void ModifyAttributes(inout Attributes attributes)
    {

    }

    // VertexDescription:
    // Transform VertexPosition, VertexNormal, VertexTangent in World Space
    #define USE_VERTEXDESCRIPTION
    void VertexDescriptionFunction(Attributes attributes, inout VertexDescription description)
    {
        
    }

    // Modify varyings at the end
    #define USE_MODIFYVARYINGS
    void ModifyVaryings(Attributes attributes, VertexDescription description, inout Varyings varyings)
    {
        varyings.interp0.xy = TRANSFORM_TEX(attributes.uv0.xy, _MainTex);
    }


    // SurfaceDescription:
    // Albedo, Normal, Metallic, Emission, Smoothness, Occlusion
    // Alpha, AlphaClipThreshold, Reflectance
    #define USE_SURFACEDESCRIPTION
    void SurfaceDescriptionFunction(Varyings IN, inout SurfaceDescription surface)
    {
        float2 uv = IN.interp0.xy;
        half4 mainTex = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, uv);
        mainTex.rgb = lerp(dot(mainTex.rgb, half3(0.2125, 0.7154, 0.0721)), mainTex.rgb, _AlbedoSaturation);
        mainTex *= _Color;

        half4 normalMap = SAMPLE_TEXTURE2D(_BumpMap, sampler_BumpMap, uv);
        surface.Normal = UnpackNormalScale(normalMap, _BumpScale);

        half4 moas = SAMPLE_TEXTURE2D(_MetallicGlossMap, sampler_MetallicGlossMap, uv);
        moas.r *= _Metallic;
        moas.a *= _Glossiness;

        surface.Albedo = mainTex.rgb;
        surface.Metallic = moas.r;
        surface.Smoothness = moas.a;
        surface.Occlusion = moas.g;
        surface.Alpha = mainTex.a;

        #ifdef _EMISSION
            half3 emissionMap = SAMPLE_TEXTURE2D(_EmissionMap, sampler_EmissionMap, uv).rgb;
            surface.Emission = emissionMap * _EmissionColor;
        #endif

        surface.SpecularOcclusion = _SpecularOcclusion;
    }

    #if defined(UNITY_PASS_FORWARD)
    #define USE_MODIFYLIGHTING
    void ModifyLighting(inout GIData giData, Varyings IN, ShaderData sd, SurfaceDescription surfaceDescription)
    {

    }
    #endif
    

CODE_END
